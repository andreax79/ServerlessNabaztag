#include protos/meteo_protos.mtl
#include protos/sleep_protos.mtl
#include protos/audiolib_protos.mtl

//--------------------------------------------------------------------------------------------------
// HTTP Server
//--------------------------------------------------------------------------------------------------

type Sock=[sockInput sockSize sockOutput sockIndex sockCloseAfter sockCallback];;

const HTTP_200 = "200 OK";;
const HTTP_204 = "204 No Content";;
const HTTP_404 = "404 Not Found";;
const http_sep="\13\n\13\n";; // separator
const http_content_start="ontent-Length: ";;
const http_content_start2="ontent-length: ";;
const http_content_end="\13\n";;

fun http_header http_status content_type=
    strcatlist
        "HTTP/1.0 "::http_status::
        "\13\10Server: MTL HTTP server"::
        (if content_type!=nil then (strcat "\13\10Content-Type: " content_type) else "")::
        "\13\10\13\10"::nil
;;

fun httpgetheader res =
    let strstr res http_sep 0 -> i in
    if i!=nil then strsub res 0 i;;

fun findsize s=
    let httpgetheader s -> header in
    if header!=nil then
    let strstr header http_content_start 0 -> i in
    let if i!=nil then i else strstr header http_content_start2 0 -> i in
    let if i!=nil then strstr header http_content_end i -> j in
    (strlen header)+(strlen http_sep)+if i!=nil then
        let i+strlen http_content_start -> i in atoi strsub header i if j!=nil then j-i;;

fun tcpwrite cnx sock=
    if sock.sockOutput!=nil then
    (
        if sock.sockIndex==nil then set sock.sockIndex=0;
        Iecholn set sock.sockIndex=writetcp cnx sock.sockOutput Iecholn sock.sockIndex;
        if sock.sockIndex!=nil && sock.sockIndex>=strlen sock.sockOutput then
        (   set sock.sockIndex=nil;
            set sock.sockOutput=nil;
            if sock.sockCloseAfter==1 then closetcp cnx
        )
    );
    0;;

fun tcpsend cnx sock s =
    set sock.sockOutput=strcat sock.sockOutput s;
    tcpwrite cnx sock;;

fun tcpcloseafter cnx sock =
    set sock.sockCloseAfter=1;
    if sock.sockIndex>=strlen sock.sockOutput then closetcp cnx
    ;;

fun tcpread cnx input sock=
    if input==nil ||0==strlen input then
    (   closetcp cnx;
        nil )
    else
    (
        if sock.sockSize==nil then
        (
            set sock.sockInput=(strcat hd sock.sockInput input)::nil;
            Secho "size=";Iecholn set sock.sockSize=findsize hd sock.sockInput;
            nil
        )
        else set sock.sockInput=input::sock.sockInput;
        if sock.sockSize!=nil && (slistlen sock.sockInput)>=sock.sockSize then
        let strcatlist rev sock.sockInput nil -> str in
        (
            set sock.sockInput=nil;
            let call sock.sockCallback [str] -> res in
            (
                tcpsend cnx sock res;
                tcpcloseafter cnx sock
            )
        )
    );
    0;;

fun tcpevent cnx val msg sock=
    if val==TCPWRITE then tcpwrite cnx sock
    else if val==TCPCLOSE then tcpread cnx nil sock
    else tcpread cnx msg sock;;

fun cbsrv cnx code msg cbrequest=
    Secholn "httpsrv accept";
    let [sockCallback:cbrequest]-> sock in
    (
        tcpcb cnx fixarg4 #tcpevent sock;
        0
    );;

fun starthttpsrv port cbrequest=
    listentcp port fixarg4 #cbsrv cbrequest;
    Secholn "starthttpsrv";
    0;;

//--------------------------------------------------------------------------------------------------

fun doublequote s=
    strcatlist "\""::s::"\""::nil
;;

fun jsonvalue k v=
    strcatlist (doublequote k)::": "::(doublequote v)::nil
;;

fun jsonnext s=
    strcat s ",\n"
;;

fun jsonlast s=
    strcat s "\n"
;;

fun jsonstatus=
    strcatlist
        (http_header HTTP_200 "application/json")::
        "{\n"::
            (jsonnext jsonvalue "mac" webmac netMac)::
            (jsonnext jsonvalue "ts" current_time_human)::
            (jsonnext jsonvalue "time" itoa time)::
            (jsonnext jsonvalue "city_code" config_get_city_code)::
            (jsonnext jsonvalue "dst" itoa config_get_dst)::
            (jsonnext jsonvalue "time_offset" itoa get_time_offset)::
            (jsonnext jsonvalue "language" config_get_lang)::
            (jsonnext jsonvalue "latitude" config_get_latitude)::
            (jsonnext jsonvalue "longitude" config_get_longitude)::
            (jsonnext jsonvalue "wake_up" itoa config_get_wake_up)::
            (jsonnext jsonvalue "go_to_bed" itoa config_get_go_to_bed)::
            (jsonnext jsonvalue "taichi" itoa infotaichi)::
            (jsonnext jsonvalue "messages" itoa infomessages)::
            (jsonnext jsonvalue "left" itoa extleft)::
            (jsonnext jsonvalue "right" itoa extright)::
            (jsonnext jsonvalue "earsmode" itoa earsmode)::
            (jsonnext jsonvalue "ear_detecting" itoa earDetecting)::
            (jsonnext jsonvalue "sleep_state" itoa gSleepState)::
            (jsonnext jsonvalue "streaming_state" itoa gStreamingState)::
            (jsonnext jsonvalue "processing_state" itoa gProcessingState)::
            (jsonnext jsonvalue "busy_state" itoa gBusyState)::
            (jsonnext jsonvalue "it_state" itoa gItState)::
            (jsonnext jsonvalue "it_app" gItApp)::
            (jsonnext jsonvalue "meteo_weather_code" itoa _meteo_weather_code)::
            (jsonlast jsonvalue "rev" BYTECODE_REVISION_STR)::
        "}"::nil
;;

//--------------------------------------------------------------------------------------------------

fun httpindex=
    strcat
        http_header HTTP_200 "text/html"
        "
#include srv/www/index.html
        "
;;

fun milligram_css=
    strcat
        http_header HTTP_200 "text/css"
        "
#include srv/www/milligram.css
        "
;;


//--------------------------------------------------------------------------------------------------

proto taichistart 0;;
proto stopEverythingAndResetToIdle 0;;
proto get_time_from_timeserver 0;;
proto audiolib_start_http 2;;
proto surprise 0;;
proto _cbWavError 0;;
proto _infosrc_update 2;;

//--------------------------------------------------------------------------------------------------

fun filterplus s=
    let strlen s -> n in
    for i=0;i<n do if (strget s i)=='+' then strset s i 32;
    s;;

fun filterpercent s i0=
    let strstr s "%" i0 -> i in
    if i==nil then (strsub s i0 nil)::nil
    else (strsub s i0 i-i0)::(ctoa htoi strsub s i+1 2)::(filterpercent s i+3);;

fun extractargs uri i=
    let strstr uri "=" i-> j in
    if j!=nil then let strstr uri "&" j-> k in
    let if k==nil then strlen uri else k -> k in
    [(strget uri i) strcatlist filterpercent filterplus strsub uri j+1 k-j-1 0]::extractargs uri k+1;;

fun extractpage uri=
    strsub uri 1 nil;;

fun uriextract uri =
    let strstr uri "?" 0 -> i in
    if i==nil then [extractpage uri nil]
    else [extractpage strsub uri 0 i extractargs uri i+1];;

var _Sargvalue;;
var _Iargvalue;;

// get a string value from the args by the key
fun Sgetargvalue args key =
    set _Sargvalue = "";
    for l=args; l!=nil; tl l do let hd l->[n v] in
    (
        if n==key then (set _Sargvalue=v)
    );
    _Sargvalue
    ;;

// get an integer value from the args by the key
fun Igetargvalue args key =
    set _Iargvalue = 0;
    for l=args; l!=nil; tl l do let hd l->[n v] in
    (
        if n==key then (set _Iargvalue=atoi v)
    );
    _Iargvalue
    ;;

fun cbhttp req=
    let strstr req " " 0 -> i in
    let strstr req " " i+1 -> j in
    let strsub req i+1 j-i-1 -> uri in
    let uriextract uri -> [page args] in
    (
        Secho "uri : "; Secholn uri;
        Secho "page : "; Secholn page;

        // misc
        if !strcmp page "update-time" then (  // get time from server
            get_time_from_timeserver;
            http_header HTTP_204 nil
        )
        else if !strcmp page "update-weather" then (  // get weather info
            meteo_get;
            http_header HTTP_204 nil
        )
        else if !strcmp page "stop" then (  // stop everything we can be doing and resets to either idle or asleep
            stopEverythingAndResetToIdle;
            http_header HTTP_204 nil
        )
        else if !strcmp page "reboot" then (  // reboot
            reboot 0x0407FE58 0x13fb6754;
            nil
        )
        else if !strcmp page "surprise" then (
            if !gSleepState then surprise;
            http_header HTTP_204 nil
        )
        else if !strcmp page "status" then (  // json status
            jsonstatus
        )
        else if !strcmp page "communication" then (  // play sound
            if !gSleepState then (controlsound midi_communion);
            http_header HTTP_204 nil
        )
        else if !strcmp page "ack" then (
            if !gSleepState then (controlsound midi_ack);
            http_header HTTP_204 nil
        )
        else if !strcmp page "abort" then (
            if !gSleepState then (controlsound midi_abort);
            http_header HTTP_204 nil
        )
        else if !strcmp page "ministop" then (
            controlsound midi_ministop;
            http_header HTTP_204 nil
        )
        else if !strcmp page "play" then (
            if !gSleepState then (audiolib_start_http Sgetargvalue args 'u' #_cbWavError);
            http_header HTTP_204 nil
        )
        else if !strcmp page "say" then (
            if !gSleepState then (audiolib_start_http (strcatlist "http://translate.google.com/translate_tts?ie=UTF-8&total=1&idx=0&textlen=32&client=tw-ob&tl=En-gb&q="::(strreplace (Sgetargvalue args 't') " " "+")::nil) #_cbWavError);
            http_header HTTP_204 nil
        )
        else if !strcmp page "taichi" then let Igetargvalue args 'v' -> v in (  // taichi
            if v == 1000 && !gSleepState then
                taichistart
            else
                set infotaichi = v;
            http_header HTTP_204 nil
        )
        else if !strcmp page "mp3" then (
            audiolib_start_http Sgetargvalue args 'v' #_cbWavError;
            http_header HTTP_204 nil
        )
        else if !strcmp page "sleep" then (  // sleep
            sleep_start;
            http_header HTTP_204 nil
        )
        else if !strcmp page "wakeup" then (  // wake up
            sleep_end;
            http_header HTTP_204 nil
        )
        else if !strcmp page "left" then (  // p=position d=direction
            if !gSleepState then (earGo 0 Igetargvalue args 'p' Igetargvalue args 'd');
            http_header HTTP_204 nil
        )
        else if !strcmp page "right" then (  // p=position d=direction
            if !gSleepState then (earGo 1 Igetargvalue args 'p' Igetargvalue args 'd');
            http_header HTTP_204 nil
        )
        else if !strcmp page "nose" then (  // v (0-4)
            set infomessages = Igetargvalue args 'v';
            http_header HTTP_204 nil
        )
        else if !strcmp page "weather" then (  // weather (0-5)
            if !gSleepState then (_infosrc_update 1 Igetargvalue args 'v');
            http_header HTTP_204 nil
        )
        else if !strcmp page "stock" then (  // stock market (0-6)
            if !gSleepState then (_infosrc_update 2 Igetargvalue args 'v');
            http_header HTTP_204 nil
        )
        else if !strcmp page "traffic" then (  // trafic (0-6)
            if !gSleepState then (_infosrc_update 3 Igetargvalue args 'v');
            http_header HTTP_204 nil
        )
        else if !strcmp page "mail" then (  // mail (0-4)
            if !gSleepState then (_infosrc_update 6 Igetargvalue args 'v');
            http_header HTTP_204 nil
        )
        else if !strcmp page "pollution" then (  // pollutions (0-10)
            if !gSleepState then (_infosrc_update 7 Igetargvalue args 'v');
            http_header HTTP_204 nil
        )
        else if !strcmp page "clear" then (
            for i=1;i<=8 do (_infosrc_update 0 i);
            http_header HTTP_204 nil
        )
        else if !strcmp page "setup" then (
            config_set_lang Sgetargvalue args 'l';            // set language
            config_set_city Sgetargvalue args 'c';            // set city code
            config_set_dst atoi Sgetargvalue args 'd';        // set Daylight Saving Time
            config_set_latitude Sgetargvalue args 'j';        // set latitude
            config_set_longitude Sgetargvalue args 'k';       // set longitude
            config_set_wake_up atoi Sgetargvalue args 'w';    // set wake up time
            config_set_go_to_bed atoi Sgetargvalue args 'b';  // set go to bed time
            config_save;
            meteo_get;
            http_header HTTP_204 nil
        )
        else if !strcmp page "milligram.css" then (
            milligram_css
        )
        else if !strcmp page "" then (  // index page
            httpindex
        )
        else (
            strcat (http_header HTTP_404 "text/plain") "Not Found"
        )
    );;


fun startwebserver port =
/*
    let uriextract "/123?a=ciccio&b=2" -> [page args] in
    let Sgetargvalue args 'a' -> a in
    let Igetargvalue args 'b' -> b in
    (
        Secho "++++++++++++page : "; Iecholn page;
        //for l=args;l!=nil;tl l do let hd l->[n v] in
        //  (Iecho n; Secho ":";Secho v;Secholn "<");
        //Secholn getargvalue args 97;
        Secholn a;
        Iecholn Igetargvalue args 'x';
        starthttpsrv port #cbhttp
    )
    ;;
*/
    // Secho jsonstatus;
    starthttpsrv port #cbhttp;;


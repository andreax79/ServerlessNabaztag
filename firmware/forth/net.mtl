/**
 * Network words for Forth interpreter
 * Copyright (c) 2025 Andrea Bonomi
 * This code is licensed under the MIT License
 */

fun _forth_http_req_cb http res f=
    let http_split_header_content res -> [header content] in (
        forth_push f [str:content];
        forth_push f [str:header];
        forth_interpreter_resume f;  // resume the interpreter
        0
    );;

/**
 * HTTP-GET ( url -- content header )
 * Perform an HTTP GET request and return the content as a string
 * HTTPS is not supported
 */
fun forth_http_get f=
    let forth_pop f -> url in
	http_request "GET" url.str nil (fixarg3 #_forth_http_req_cb f) HTTP_NORMAL;
    nil;;

/**
 * HTTP-POST ( payload url -- content header )
 * Perform an HTTP POST request and return the content as a string
 * HTTPS is not supported
 */
fun forth_http_post f=
    let forth_pop f -> url in
    let forth_pop f -> payload in
	http_request "POST" url.str payload.str (fixarg3 #_forth_http_req_cb f) HTTP_NORMAL;
    nil;;

/**
 * MAC ( -- mac )
 * Push the MAC address of the WIFI interface onto the stack
 */
fun forth_mac f=
    forth_push f [str:wifi_mac_addr];;

/**
 * IP ( -- ip )
 * Push the IP address of the WIFI interface onto the stack
 */
fun forth_ip f=
    forth_push f [str:ip_to_str netip];;


/**
 * TCP-LISTEN ( text port name -- )
 * Start a TCP server listening on the given port with the given name
 */
fun forth_tcp_listen f=
    let forth_pop f -> name in // unused
    let forth_pop f -> port in
    let forth_pop f -> text in
    tcp_listen port.int fixarg4 #telnet_server_cb text.str;
    nil;;


/**
 * Handle HTTP callback for loading Forth code from URL.
 */
fun _forth_load_cb http res cb=
    if res != nil then
        forth_interpreter_ex (http_get_content res) nil nil nil cb;;

/**
 * Load and execute a Forth code from a URL.
 */
fun forth_load url cb=
    http_request "GET" url nil (fixarg3 #_forth_load_cb cb) HTTP_NORMAL;;
